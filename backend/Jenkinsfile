properties([
    copyArtifactPermission('/FurnitureShop/Production/*')
]);

def isReleaseBuild = BRANCH_NAME == 'master';
def mssqlPassword = 'Passw12#'

leancode.builder('furnitureshop-backend')
    .withMonorepoTest(['backend'])
    .withDocker()
    .withDotnet([version: '5.0', image: 'mcr.microsoft.com/dotnet/sdk'])
    .withDefaultContainer(
        name: 'mssql',
        image: 'leancode.azurecr.io/mssql',
        version: 'latest',
        defCpuReq: '1',
        defCpuLim: '2',
        defMemReq: '1Gi',
        defMemLim: '2Gi',
        args: [:])
    .withEnv(envVar(key: 'ACCEPT_EULA', value: 'Y'))
    .withEnv(envVar(key: 'SA_PASSWORD', value: mssqlPassword))
    .withVolume(emptyDirVolume(mountPath: '/var/opt/mssql', memory: true))
    .run {

    def scmVars

    stage('Checkout') {
        scmVars = safeCheckout scm
    }

    leancode.configureRepositories()

    stage('Version') {
        env.GIT_COMMIT = scmVars.GIT_COMMIT
        env.APP_VERSION = isReleaseBuild ? "0.1.${nextBuildNumber()}" : '0.0.0'
        echo "Building version: ${env.APP_VERSION}"

        if (isReleaseBuild) {
            currentBuild.displayName = "v${env.APP_VERSION}"
        }
    }

    dir('backend') {
        container('dotnet') {
            stage('Build') {
                sh '''
                dotnet restore
                dotnet build -c Release --no-restore
                '''
            }

            stage('Test') {
                try {
                    dir('tests') {
                        sh 'dotnet msbuild /t:RunTests /p:Configuration=Release /p:LogFileName=$PWD/test-results/tests.trx'
                    }
                } finally {
                    step([$class: 'MSTestPublisher', testResultsFile:'*/test-results/*.trx', failOnError: true, keepLongStdio: true])
                }
            }

            stage('Integration test') {
                try {
                    dir('tests/FurnitureShop.IntegrationTests') {
                        env.Domains__Public = 'localhost'
                        env.Domains__ApiInternal = 'localhost'
                        env.ConnectionStrings__DatabaseBase = "Server=localhost,1433;User Id=sa;Password=$mssqlPassword;Connection Timeout=60"

                        sh 'dotnet test -c Release -l "trx;LogFileName=$PWD/test-results/integration.trx" -p:MaxParallelThreads=2'
                    }
                } finally {
                    step([$class: 'MSTestPublisher', testResultsFile:'tests/FurnitureShop.IntegrationTests/test-results/*.trx', failOnError: true, keepLongStdio: true])
                }
            }

            stage('Publish') {
                dir('src/Apps/FurnitureShop.Api') {
                    sh 'dotnet publish -c Release -o deploy --no-build'
                }
                dir('src/Apps/FurnitureShop.Migrations') {
                    sh 'dotnet publish -c Release -o deploy --no-build'
                }
            }
        }

        stage('Push containers') {
            when (isReleaseBuild) {
                leancode.withACR {
                    sh """
                        buildctl build \
                            --frontend dockerfile.v0 \
                            --local context=. \
                            --local dockerfile=./release \
                            --opt filename=Dockerfile.api \
                            --output type=image,name=${leancode.ACR()}/furnitureshop-api:$APP_VERSION,push=true
                        buildctl build \
                            --frontend dockerfile.v0 \
                            --local context=. \
                            --local dockerfile=./release \
                            --opt filename=Dockerfile.migrations \
                            --output type=image,name=${leancode.ACR()}/furnitureshop-migrations:$APP_VERSION,push=true
                    """
                }
            }
        }
    }

    dir('backend/release') {
        stage('Deploy to test') {
            when (isReleaseBuild) {
                env.ENVIRONMENT = 'test'
                env.NAMESPACE = 'furnitureshop-test'
                sh 'envsubst < migrations.yaml.tpl > migrations.yaml'
                sh 'envsubst < api.yaml.tpl > api.yaml'

                withCredentials([file(credentialsId: 'FurnitureShopTestKubeconfig', variable: 'KUBECONFIG')]) {
                    sh '''
                    kubectl delete -f migrations.yaml --ignore-not-found

                    echo "Migrating database"
                    kubectl apply -f migrations.yaml
                    kubectl wait --filename=migrations.yaml --for='condition=Complete' --timeout=180s || exit 1

                    echo "Migrations completed successfully"

                    kubectl apply -f api.yaml
                    '''
                }

                archiveArtifacts artifacts: '*.yaml', fingerprint: true
                relatedBuild.writeAsArtifact "$APP_VERSION", 'APP_VERSION'
            }
        }
    }
}
